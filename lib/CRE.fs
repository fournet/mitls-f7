module CRE

open Bytes
open TLSConstants
open TLSInfo
open TLSPRF

type rsarepr = bytes
type rsapms = {rsapms: rsarepr}
type dhpms = {dhpms: DHGroup.elt}

#if ideal
type pms = RSA_pms of rsapms | DHE_pms of dhpms
type ext_pms = Ext_RSA_pms of RSAKey.pk * ProtocolVersion * rsapms | Ext_DHE_pms of DHGroup.p * DHGroup.g * DHGroup.elt * DHGroup.elt * dhpms


type pred = GeneratedRSAPMS of RSAKey.pk * ProtocolVersion * rsapms

// We maintain two logs:
// - a log of honest pms values
(* CF
   We need a predicate 'HonestRSAPMS', and its ideal boolean function `honest' 

   To ideally avoid collisions concerns between Honest and Coerced pms, 
   we could discard this log, and use instead a sum type of rsapms, e.g.
   type (;pk:RSAKey.pk, pv:ProtocolVersion) rsapms = 
   | IdealRSAPMS    of abstract_seed 
   | ConcreteRSAPMS of rsarepr

MK the first log is used in two idealization steps
*) 
let honest_log = ref []
let honest pms = exists (fun el -> el=pms) !honest_log

// - a log for looking up good ms values using their pms values

//MK causes problems so rewritten in terms of honest
//MK let corrupt pms = not(honest pms)

let log = ref []
#endif

let genRSA (pk:RSAKey.pk) (vc:TLSConstants.ProtocolVersion) : rsapms = 
    let verBytes = TLSConstants.versionBytes vc in
    let rnd = random 46 in
    let pms = verBytes @| rnd in
    let pms = {rsapms = pms}
    #if ideal
    if RSAKey.honest pk then honest_log := RSA_pms(pms)::!honest_log
    // event keeping track of honestly-generated PMSs
    Pi.assume (GeneratedRSAPMS(pk,vc,pms));
    #endif
    pms

let coerceRSA (pk:RSAKey.pk) (pv:ProtocolVersion) b = {rsapms = b}
let leakRSA (pk:RSAKey.pk) (pv:ProtocolVersion) pms = pms.rsapms



let sampleDH p g (gx:DHGroup.elt) (gy:DHGroup.elt) = 
    let gz = DHGroup.genElement p g in
    let pms = {dhpms = gz}
    #if ideal
    honest_log := DHE_pms(pms)::!honest_log
    #endif
    pms

let coerceDH (p:DHGroup.p) (g:DHGroup.g) (gx:DHGroup.elt) (gy:DHGroup.elt) b = {dhpms = b} 

// internal
let prfMS sinfo pmsBytes: PRF.masterSecret =
    let pv = sinfo.protocol_version in
    let cs = sinfo.cipher_suite in
    let data = csrands sinfo in
    let res = prf pv cs pmsBytes tls_master_secret data 48 in
    PRF.coerce sinfo res

(* MK assumption notes

We require prfMS to be a deterministic computational randomness extractor for both 
of the distributions generated by genRSA and sampleDH. It is sufficient for the following two 
distributions to be indistinguishable to establish using a standard hybrid argument
indistinguishability of any polynomial length sequence of the two distributions on the right 
from the same length sequence of PRF.sample.

PRF.sample si ~_C prfMS si genRSA pk vc //relate si and pk vc
PRF.sample si ~_C prfMS si sampleDH p g //relate si and p g

*)

let prfSmoothRSA si (pv:ProtocolVersion) pms = 
    #if ideal
    if honest (RSA_pms(pms))
    then match tryFind (fun (el1,el2,_) -> el1 = csrands si && el2 = RSA_pms(pms) ) !log with
             Some(_,_,ms) -> ms
           | None -> 
                 let ms=PRF.sample si 
                 log := (csrands si, RSA_pms(pms),ms)::!log
                 ms 
    else prfMS si pms.rsapms
    #else
    prfMS si pms.rsapms
    #endif

let prfSmoothDHE si (p:DHGroup.p) (g:DHGroup.g) (gx:DHGroup.elt) (gy:DHGroup.elt) (pms:dhpms) =
    //#begin-ideal 
    #if ideal
    if honest(DHE_pms(pms))
    then match tryFind (fun (el1,el2,_) -> el1 = csrands si && el2 = DHE_pms(pms)) !log  with
             Some(_,_,ms) -> ms
           | None -> 
                 let ms=PRF.sample si 
                 log := (csrands si, DHE_pms(pms),ms)::!log;
                 ms 
    else prfMS si pms.dhpms
    //#end-ideal
    #else
    prfMS si pms.dhpms
    #endif


